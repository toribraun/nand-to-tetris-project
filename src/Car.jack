class Car {
    field int trackNumber;
    field int x, y;
    field int width, height;
    field int gap;

    field int blockSize;

    constructor Car new(int _width, int _height, int _blockSize, int _gap) {
        let trackNumber = 0;
        let blockSize = _blockSize;
        let width = _width;
        let height = _height;
        let x = (512 / 16) / 2 - width;
        let y = 12;  // todo почему так?
        let gap = _gap;
        return this;
    }

    // меняет трек и координату x = x - width 
    method void ChangeTrackLeft() {
        if (trackNumber = 0) {
            return;
        }
        let trackNumber = trackNumber - 1;
        let x = x - width;
        return;
    }

    // меняет трек и координату x = x + width 
    method void ChangeTrackRight(int tracksCount) {
        if (trackNumber = (tracksCount - 1)) {
            return;
        }
        let trackNumber = trackNumber + 1;
        let x = x + width;
        return;
    }

    method boolean CheckCollision(OpponentCar opponentCar) {
        return (((Math.abs(opponentCar.GetY() - y) < height) | (Math.abs(opponentCar.GetY() - y) = height)) & (Math.abs(opponentCar.GetX() - x) < width));
    }

    method void Draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x * blockSize, y * blockSize, (x + width) * blockSize - (blockSize / gap), (y + height) * blockSize - 1);
        return;
    }

    method void dispose() {  // todo освобождать 
		do Memory.deAlloc(this);
		return;
	}
}
